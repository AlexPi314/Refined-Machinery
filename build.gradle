buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

version = '${mod_version}'
group = 'abused_master.refinedmachinery'
archivesBaseName = 'refinedmachinery'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	mappings channel: 'snapshot', version: '20180921-1.13'
	// accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'

			mods {
				refinedmachinery {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'

			mods {
				refinedmachinery {
					source sourceSets.main
				}
			}
		}
	}
}

repositories {
	maven { url = "http://maven.abusedmaster.xyz/" }
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.13.2-25.0.215'
	//compile "abused_master.abusedlib:AbusedLib:${abusedlib_version}"
}

jar {
	manifest {
		attributes([
				"Specification-Title": "Refined Machinery",
				"Specification-Vendor": "abused_master",
				"Specification-Version": "1",
				"Implementation-Title": project.name,
				"Implementation-Version": "${version}",
				"Implementation-Vendor" :"abused_master",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives sourcesJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
			artifact sourcesJar
			pom {
				name = "Refined-Machinery"
				url = "https://github.com/NerdHubMC/Refined-Machinery"
				licenses {
					license {
						name = "MIT License"
						url = "https://github.com/NerdHubMC/Refined-Machinery/blob/master/LICENSE.md"
					}
				}
				scm {
					url = "https://github.com/NerdHubMC/Refined-Machinery"
					connection = "scm:git:git://github.com/NerdHubMC/Refined-Machinery.git"
					developerConnection = "scm:git:git@github.com:NerdHubMC/Refined-Machinery.git"
				}
				issueManagement {
					system = "github"
					url = "https://github.com/NerdHubMC/Refined-Machinery/issues"
				}
			}
		}
	}

	repositories {
		if (project.hasProperty("maven_user") && project.hasProperty("maven_pass")) {
			maven {
				name = "Abused's Maven"
				url = findProperty("maven_url")
				credentials {
					username = findProperty("maven_user")
					password = findProperty("maven_pass")
				}
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}
}
tasks.publish.dependsOn build